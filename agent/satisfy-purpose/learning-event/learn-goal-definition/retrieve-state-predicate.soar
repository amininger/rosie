# Retrieve info for state predicates on the goal
sp {learn-goal-definition*retrieve*smem*state*predicate*info
   (state <s> ^name learn-goal-definition
              ^generalize-task-goal.task-goal.<gpnum> <gp>)
   (<gp> ^type state
         ^handle <handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 2
            ^destination <s>
            ^result-name state-predicate-info)
   (<cue> ^handle <handle>)
}

sp {learn-goal-definition*elaborate*generalize-task-goal*state*predicate*property-handle
   (state <s> ^name learn-goal-definition
              ^generalize-task-goal.task-goal.<gpnum> <gp>
              ^state-predicate-info <info>)
   (<gp> ^type state
         ^handle <handle>)
   (<info> ^handle <handle>
           ^property.handle <prop-h>)
-->
   (<gp> ^property-handle <prop-h>)
}

# Retrieve info for condition state predicates
sp {learn-goal-definition*retrieve*smem*state*predicate*info*conditions
   (state <s> ^name learn-goal-definition
              ^generalize-task-argument <gta>)
   (<gta> ^arg-type predicate
          ^instantiation <i>)
   (<i> ^type state 
        ^handle <pred-h>
       -^property-handle)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 2
            ^destination <s>
            ^result-name state-predicate-info)
   (<cue> ^handle <pred-h>)
}

sp {learn-goal-definition*elaborate*generalize-task-argument*state*predicate*property-handle
   (state <s> ^name learn-goal-definition
              ^generalize-task-argument <gta>
              ^state-predicate-info <info>)
   (<gta> ^arg-type predicate
          ^instantiation <i>)
   (<i> ^type state
        ^handle <pred-h>)
   (<info> ^handle <pred-h>
           ^property.handle <prop-h>)
-->
   (<i> ^property-handle <prop-h>)
}
